###############################################################################
#
# cfe_internal_management
# - manage log rotation
# - manage to many processes
#
###############################################################################
bundle agent cfe_internal_management
{
  methods:

    any::

      "any"
        comment   => "Rotate CFEngine logs so we dont fill the disk",
        handle    => "cfe_internal_management_log_rotation",
        usebundle => cfe_internal_log_rotation;
}

###############################################################################
#
# cfe_internal_limit_robot_agents
# - kill CFE processes and restart it when the processes grow
#
###############################################################################
bundle agent cfe_internal_limit_robot_agents
{
  processes:
    
    !windows::
      
      "bin/cf-execd"
        process_count => check_execd("1"),
        comment       => "Check cf-execd process if exceed the number",
        handle        => "cfe_internal_limit_robot_agents_processes_check_cf_execd";

      "bin/cf-monitord"
        process_count => check_monitord("1"),
        comment       => "Check cf-monitord process if exceed the number",
        handle        => "cfe_internal_limit_robot_agents_processes_check_cf_monitord";

    something_wrong_execd::
    
      "bin/cf-execd"
        signals       => { "term", "kill" },
        restart_class => "restart_execd",
        comment       => "When cf-execd comes undone then kill all and restart the process",
        handle        => "cfe_internal_limit_robot_agents_processes_kill_cf_execd";

    something_wrong_monitord::

      "bin/cf-monitord"
        signals       => { "term", "kill" },
        restart_class => "restart_monitord",
        comment       => "When cf-monitord comes undone then kill all and restart the process",
        handle        => "cfe_interal_limit_robot_agents_processes_kill_cf_monitord";

  commands:
  
    restart_execd::

      "${sys.cf_execd}"
        comment => "Restart cf-execd process",
        classes => kept_successful_command,
        handle  => "cfe_internal_limit_robot_agents_commands_restart_cf_execd";

    restart_monitord::

      "${sys.cf_monitord}"
        comment => "Restart cf-monitord process",
        classes => kept_successful_command,
        handle  => "cfe_internal_limit_robot_agents_commands_restart_cf_monitord";
}

body process_count check_execd(n)
{
  match_range         => "0,${n}";
  out_of_range_define => { "something_wrong_execd" };
}

body process_count check_monitord(n)
{
  match_range         => "0,${n}";
  out_of_range_define => { "something_wrong_monitord" };
}

###############################################################################
#
# cfe_internal_log_rotation
# - Rotate logs and clean up old files so the disk doesnt get full
#
###############################################################################
bundle agent cfe_internal_log_rotation
{
  methods:
    
    enable_cfe_internal_log_rotation::

      "rotate_promise_summary"
        comment   => "Rotate log files once their size reach 1MB, keep 10 versions";
        usebundle => logrotate("@{def.cfe_log_files}", "1m", "10");

      "rotate_outpus"
        comment   => "Delete outpus/* and reports/* files older than 7 days",
        usebundle => prunedir("@{def.cfe_log_dirs}", "7");
}
