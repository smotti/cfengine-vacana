body common control
{
  inputs => {
    "${sys.libdir}/stdlib.cf",
    "${sys.libdir}/vacana/lib.cf",
  };

  bundlesequence => { "test_va_command_in_shell" };
}

bundle agent test_va_command_in_shell
{
  meta:

    "purpose" string => "Test bundle va_command_in_shell";
    "tags"     slist => { "commands", "test", "vacana"};

  vars:

    "test_file" string => "/tmp/hello";
    "command"   string => "/bin/echo hello > ${test_file}";
    "codes"       data => parsejson(
      '{ "kept": "0", "repaired": "0", "failed": "1" }'
    );
    "promisee"  string => "test_va_command_in_shell";

    "ccommand"   string => canonify("${command}");
    "ctest_file" string => canonify("${test_file}");

  classes:

    "test_pass" and => {
      classmatch("va_command_in_shell_${ccommand}_(kept|repaired)"),
      "va_file_remove_${ctest_file}_repaired"
    };

  methods:

    "test_va_command_in_shell_run"
      comment   => "Call bundle for test",
      handle    => "test_va_command_in_shell_methods_run_bundle",
      usebundle => va_command_in_shell("${command}", "@{codes}", "${promisee}");

    "test_va_command_in_shell_cleanup"
      comment   => "Cleanup artifacts created by test",
      handle    => "test_va_command_in_shell_methods_cleanup",
      usebundle => va_file_remove("${test_file}", "${promisee}");

  reports:

    test_verbose::

      "PASS: test_va_command_in_shell_run"
        ifvarclass => "test_pass";

    any::

      "FAIL: test_va_command_in_shell - commands promise failed"
        ifvarclass => "va_command_in_shell_${ccommand}_failed";

      "FAIL: test_va_command_in_shell - Failed to cleanup"
        ifvarclass => "va_file_remove_${ctest_file}_failed";
}
