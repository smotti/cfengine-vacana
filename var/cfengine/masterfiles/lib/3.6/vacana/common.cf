bundle common va_c
{
  meta:

    "purpose" string => "Common classes and variables for vacana lib.";
    "tags" slist     => { "vacana" };

  vars:
    
    "promise_log"
      comment => "File to log promises to without their outcome",
      string  => "/var/cfengine/vacana/promises.log";

    "outcome_log"
      comment => "File to log promises with their outcome, used by promises_outcome bundle",
      string  => "/var/cfengine/vacana/promises_outcome.log";

    "client_logs"
      comment => "Directory where to put the client logs on the policy hub",
      string  => "/var/cfengine/vacana/clients";

    "log_delim"
      comment => "Delimiter used in promise logs",
      string  => "\s*;;\s*";

    "max_num"
      comment => "Maximum number of lines to read from a file",
      int     => "300";

    "max_bytes"
      comment => "Maximum number of bytes to read from a file",
      string  => "2M";
}


###############################################################################
#
# actions
#
###############################################################################

# TODO: Add bundle to create log files, if they don't exist, and if they do
#	remove them.

body action va_log_promise(handle, promiser, promisee)
# @brief  Log a string to va_c.promise_log with given params.
# @param  handle    Promise handle
# @param  promiser  Promiser of the promise
# @param  promisee  Who makes the promise
{
  log_kept     => "${va_c.promise_log}";
  log_repaired => "${va_c.promise_log}";
  log_failed   => "${va_c.promise_log}";
  log_string   => "${handle} ;; ${promiser} ;; ${promisee}";
}


###############################################################################
#
# classes
#
###############################################################################

body classes va_classes_generic(x)
# @brief  Almost like that stdlib one, but only one class for each possible outcome
#
# NOTE: Less classes means less cpu and/or memory consumption and faster
#       cf-agent execution
#
# @param x  Prefix for promise outcome classes
{
  promise_repaired  => { "${x}_repaired" };
  repair_failed     => { "${x}_failed" };
  repair_denied     => { "${x}_failed" };
  repair_timeout    => { "${x}_failed" };
  promise_kept      => { "${x}_kept" };
}

body classes va_classes_command(prefix, kept, repaired, failed)
# @brief  Define promise outcomes with 'prefix' prefixed to _kept|repaired|failed
#
# NOTE: Use this for commands promises
#
# @param prefix     Prefix for outcome classes
# @param kept       Command returncode to indicate that promise was kept
# @param repaired   Command returncode to indicate that promise was repaired
# @param failed     Command returncode to indicate that promise failed
{
  promise_repaired      => { "${prefix}_repaired" };
  repair_failed         => { "${prefix}_failed" };
  repair_denied         => { "${prefix}_failed" };
  repair_timeout        => { "${prefix}_failed" };
  promise_kept          => { "${prefix}_kept" };
  kept_returncodes      => { "${kept}" };
  repaired_returncodes  => { "${repaired}" };
  failed_returncodes    => { "${failed}" };
}
